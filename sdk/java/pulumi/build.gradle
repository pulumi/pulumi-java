plugins {
    id("signing")
    id("java-library")
    id("maven-publish")
    id("com.google.protobuf") version "0.8.19"
}

version = System.getenv("PULUMI_JAVA_SDK_VERSION") ?: "0.0.1"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url("https://maven-central.storage-download.googleapis.com/maven2/")
    }
    mavenCentral()
    mavenLocal()
}

def grpcVersion = "1.47.0"
def protobufVersion = "3.21.2"
def protocVersion = protobufVersion

dependencies {
    implementation("io.grpc:grpc-protobuf:${grpcVersion}")
    implementation("io.grpc:grpc-stub:${grpcVersion}")
    runtimeOnly("io.grpc:grpc-netty-shaded:${grpcVersion}")
    // Workaround for @javax.annotation.Generated
    // see: https://github.com/grpc/grpc-java/issues/3633
    compileOnly("javax.annotation:javax.annotation-api:1.3.2")
    implementation("com.google.code.findbugs:jsr305:3.0.2")
    implementation("net.javacrumbs.future-converter:future-converter-java8-guava:1.2.0") // TODO: remove if possible or inline
    implementation("com.google.guava:guava:31.1-jre") // make sure we don't clash with grpc deps
    implementation("com.google.code.gson:gson:2.9.0") // make sure we don't clash with grpc deps
    implementation("com.google.protobuf:protobuf-java:${protobufVersion}") // make sure we don't clash with grpc deps
    implementation("com.google.protobuf:protobuf-java-util:${protobufVersion}") // make sure we don't clash with grpc deps

    testImplementation("io.grpc:grpc-testing:${grpcVersion}")

    def junitVersion = "5.8.2"
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-params:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    testImplementation("org.assertj:assertj-core:3.23.1")
    testImplementation("org.mockito:mockito-core:4.6.1")
    testImplementation("nl.jqno.equalsverifier:equalsverifier:3.10")
    testImplementation("org.junit-pioneer:junit-pioneer:1.7.1")
}

protobuf {
    protoc {
        if (osdetector.os == "osx") {
            artifact = "com.google.protobuf:protoc:${protocVersion}:osx-x86_64"
        } else {
            artifact = "com.google.protobuf:protoc:${protocVersion}"
        }
    }
    plugins {
        grpc {
            if (osdetector.os == "osx") {
               artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}:osx-x86_64"
            } else {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
            }
        }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        proto {
            // In addition to the default 'src/main/proto'
            srcDir "$rootDir/../../pulumi/sdk/proto"
        }
        java {
            srcDirs "src/main/java"
            srcDirs "build/generated/source/proto/main/grpc"
            srcDirs "build/generated/source/proto/main/java"
        }
    }
    pub {
        java {
            srcDirs "src/main/java"
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'

        // set options for log level LIFECYCLE
        events = ['failed']

        info {
            events = ['failed', 'skipped']
        }

        debug {
            events = ['started', 'skipped', 'failed']
        }
    }

    systemProperty 'java.util.logging.config.file', "${projectDir}/src/test/resources/logging.properties"
}


def gprUser = project.findProperty("gpr.user")  ?: System.getenv("GPR_USER")
def gprToken = project.findProperty("gpr.token") ?: System.getenv("GPR_TOKEN")
def ossrhRepoUrl = project.findProperty("ossrhRepoUrl") ?: System.getenv("OSSRH_REPO_URL")
def ossrhUsername = project.findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
def ossrhPassword = project.findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")

task sourcesJar(type: Jar) {
    from sourceSets.pub.java
    archiveClassifier.set('sources')
}

publishing {
    publications {
        gpr(MavenPublication) {
            groupId = "com.pulumi"
            artifactId = "pulumi"
            version = version
            from components.java
            artifact sourcesJar

            pom {
                inceptionYear = "2022"
                name = "pulumi"
                packaging = "jar"
                description = "Pulumi Java SDK"

                url = "https://github.com/pulumi/pulumi-java"

                scm {
                    connection = "git@github.com:pulumi/pulumi-java.git"
                    developerConnection = "git@github.com:pulumi/pulumi-java.git"
                    url = "https://github.com/pulumi/pulumi-java"
                }

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id = "pulumi"
                        name = "Pulumi"
                        email = "support@pulumi.com"
                    }
                }
            }
        }
    }

    if (ossrhUsername) {
        repositories {
            maven {
                name = "OSSRH"
                url = ossrhRepoUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
       }
    } else {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/pulumi/pulumi-java")
                credentials {
                    username = gprUser
                    password = gprToken
                }
            }
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    options.jFlags("-Xmx2g", "-Xms512m")

    exclude("com/google/rpc")
    exclude("com/pulumi/bootstrap/internal")
    exclude("com/pulumi/codegen/internal")
    exclude("com/pulumi/context/internal")
    exclude("com/pulumi/core/internal")
    exclude("com/pulumi/deployment/internal")
    exclude("com/pulumi/internal")
    exclude("com/pulumi/resources/internal")
    exclude("com/pulumi/serialization/internal")
    exclude("pulumirpc")
}

def signingKeyId = project.findProperty("signingKeyId") ?: System.getenv("SIGNING_KEY_ID")
def signingKey = project.findProperty("signingKey") ?: System.getenv("SIGNING_KEY")
def signingPassword = project.findProperty("signingPassword") ?: System.getenv("SIGNING_PASSWORD")

if (ossrhUsername) {
    signing {
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign publishing.publications.gpr
    }
}
