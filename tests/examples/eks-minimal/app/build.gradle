plugins {
    id 'application'
}

var gprUser = project.findProperty("gpr.user")  ?: System.getenv("GPR_USER")
var gprToken = project.findProperty("gpr.token") ?: System.getenv("GPR_TOKEN")

repositories {
    mavenLocal()
    maven { // The google mirror is less flaky than mavenCentral()
        url("https://maven-central.storage-download.googleapis.com/maven2/")
    }
    mavenCentral()

    if (gprUser) {
        repositories {
            maven {
                url = uri("https://maven.pkg.github.com/pulumi/pulumi-java")
                credentials {
                    username = gprUser
                    password = gprToken
                }
            }
        }
    }
}

var pulumiJavaSdkVersion = System.getenv("PULUMI_JAVA_SDK_VERSION") ?: "0.0.1-SNAPSHOT"
var pulumiKubernetesSdkVersion = System.getenv("PULUMI_KUBERNETES_PROVIDER_SDK_VERSION") ?: "3.15.1";
var pulumiAwsSdkVersion = System.getenv("PULUMI_AWS_PROVIDER_SDK_VERSION") ?: "4.37.3";
var pulumiEksSdkVersion = System.getenv("PULUMI_EKS_PROVIDER_SDK_VERSION") ?: "0.37.1";

dependencies {
    implementation "com.pulumi:pulumi:$pulumiJavaSdkVersion"
    implementation "com.pulumi:kubernetes:$pulumiKubernetesSdkVersion"
    implementation "com.pulumi:aws:$pulumiAwsSdkVersion"
    implementation "com.pulumi:eks:$pulumiEksSdkVersion"
}

application {
    mainClass = project.hasProperty("mainClass")
            ? project.getProperty("mainClass")
            : 'com.pulumi.example.eksminimal.App'
}
